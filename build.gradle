plugins {
  id 'java-platform'
  id 'com.diffplug.spotless' version '5.8.2' apply false
  id 'net.ltgt.errorprone' version '1.3.0' apply false
}

ext {
  hamcrestVersion = '2.2'
  junitVersion = '5.7.0'
  mockitoVersion = '3.6.28'
  slf4jVersion = '1.7.30'
}

allprojects {
  apply plugin: 'maven-publish'

  repositories {
    mavenLocal()
    mavenCentral()
  }
}

group = 'org.dacci.michelle'
version = '1.0.0-SNAPSHOT'

dependencies {
  constraints {
    subprojects {
      api it
    }
  }
}

publishing {
  publications {
    mavenPlatform(MavenPublication) {
      artifactId = 'bom'
      from components.javaPlatform
    }
  }
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'jacoco'
  apply plugin: 'com.diffplug.spotless'
  apply plugin: 'net.ltgt.errorprone'

  group = rootProject.group
  version = rootProject.version
  sourceCompatibility = '11'

  configurations {
    testImplementation {
      extendsFrom compileOnly
    }
  }

  dependencies {
    implementation platform('com.fasterxml.jackson:jackson-bom:[2.0,3.0)')
    implementation platform("org.junit:junit-bom:$junitVersion")

    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    errorprone 'com.google.errorprone:error_prone_core:latest.release'
  }

  java {
    withSourcesJar()
  }

  spotless {
    java {
      googleJavaFormat()
      importOrderFile "$rootDir/dacci.importorder"
    }
  }

  compileJava.options.encoding = 'UTF-8'
  compileTestJava.options.encoding = 'UTF-8'

  test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
  }

  jacocoTestReport {
    test
  }

  javadoc {
    options {
      encoding = 'UTF-8'
    }
  }

  publishing {
    publications {
      maven(MavenPublication) {
        artifactId = project.path.substring(1).replace(':', '-')
        from components.java
      }
    }
  }
}
